include(BundleUtilities)
set(BU_CHMOD_BUNDLE_ITEMS ON)

# Do not use @CMAKE_INSTALL_PREFIX@ since this file will be
# configured before cpack stage

# Forward empty variables
set(CMAKE_MACOSX_BUNDLE @CMAKE_MACOSX_BUNDLE@)
set(CMAKE_EXECUTABLE_SUFFIX @CMAKE_EXECUTABLE_SUFFIX@)

if(CMAKE_MACOSX_BUNDLE)
  set(qt_conf_dest "TextFinder.app/Contents/Resources")
else()
  set(qt_conf_dest "bin")
endif()

file(WRITE "${CMAKE_INSTALL_PREFIX}/${qt_conf_dest}/qt.conf" "")

set(app "TextFinder${CMAKE_EXECUTABLE_SUFFIX}")
if(CMAKE_MACOSX_BUNDLE)
  set(app "${app}.app")
else()
  set(app "bin/${app}")
endif()
set(app "${CMAKE_INSTALL_PREFIX}/${app}")

set(platforms "${CMAKE_INSTALL_PREFIX}/@platform_dest@/platforms")
file(GLOB_RECURSE QTPLUGINS "${platforms}/*${CMAKE_SHARED_LIBRARY_SUFFIX}")

fixup_bundle("${app}" "${QTPLUGINS}" "")
